<xsl:transform xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0">
  <xsl:output method="text"/>
  <xsl:template match="DATA">
    <html>
      <head>
        <title>ABAP Time Machine</title>
        <link href="abaptimemachine.css" rel="stylesheet" type="text/css"/>
      </head>
      <body>
        <xsl:call-template name="header"/>
        <xsl:call-template name="options"/>
        <xsl:call-template name="toc"/>
        <xsl:call-template name="parts"/>
        <xsl:call-template name="stats"/>
        <xsl:call-template name="footer"/>
      </body>
    </html>
  </xsl:template>

  <xsl:template name="header">
    <xsl:param name="node" select="." />
    <div class="header">
      <h1>
        <xsl:choose>
          <xsl:when test="TYPE = 'CLAS'">Class</xsl:when>
          <xsl:when test="TYPE = 'PROG'">Program</xsl:when>
          <xsl:when test="TYPE = 'FUGR'">Function group</xsl:when>
          <xsl:when test="TYPE = 'FUNC'">Function module</xsl:when>
          <xsl:when test="TYPE = 'TR'">Transport request</xsl:when>
        </xsl:choose>
        <xsl:text> </xsl:text>
        <xsl:value-of select="NAME"/>
      </h1>
      <div>&amp;nbsp;</div>
      <div class="header_text"><xsl:value-of select="HEADER_TEXT"/></div>
      <div>&amp;nbsp;</div>
    </div>
  </xsl:template>

  <xsl:template name="options">
    <xsl:param name="node" select="." />
    <div class="metadata">
    <form method="get" action="SAPEVENT:timetravel">
      <label for="timestamps">Moment: </label>
      <select id="timestamp" name="timestamp">
        <xsl:for-each select="TIMESTAMPS/TIMESTAMP">
          <option>
            <xsl:attribute name="value">
              <xsl:value-of select="."/>
            </xsl:attribute>
            <xsl:if test=". = ../../TIMESTAMP">
              <xsl:attribute name="selected">selected</xsl:attribute>
            </xsl:if>
            <xsl:value-of select="concat(substring(.,1,4),'-',substring(.,5,2),'-',substring(.,7,2))"/>
            &amp;nbsp;
            <xsl:value-of select="concat(substring(.,9,2),':',substring(.,11,2),':',substring(.,13,2))"/>
          </option>
        </xsl:for-each>
      </select>
      <input type="button" value="Time travel" onclick="form.submit();"/>
    </form>
    </div>
  </xsl:template>

  <xsl:template name="toc">
    <xsl:param name="node" select="." />
    <h2>Table of contents</h2>
    <div class="toc">
      <div class="toc-child">
        <ul>
          <xsl:for-each select="PARTS/ZTIMEM_PART_SOURCE">
            <li>
              <a>
                <xsl:attribute name="href"><xsl:text>#</xsl:text><xsl:value-of select="NAME"/></xsl:attribute>
                <xsl:value-of select="NAME"/>
              </a>
            </li>
          </xsl:for-each>
        </ul>
      </div>
      <div class="toc-child">
        <ul>
          <li>
            <a>
              <xsl:attribute name="href">#stats</xsl:attribute>
              Statistics
            </a>
          </li>
        </ul>
      </div>
    </div>
  </xsl:template>

  <xsl:template name="parts">
    <xsl:param name="node" select="." />
    <xsl:for-each select="PARTS/ZTIMEM_PART_SOURCE">
      <h2 class="blame">
        <a>
          <xsl:attribute name="name"><xsl:value-of select="NAME"/></xsl:attribute>
          <xsl:value-of select="NAME"/>
        </a>
      </h2>
      <table class="blame">
        <THEAD class="header">
          <TR>
            <TH class="num">Line</TH>
            <TH>
              <a>
                <xsl:attribute name="href">SAPEVENT:source?<xsl:value-of select="TYPE"/>|<xsl:value-of select="OBJECT_NAME"/></xsl:attribute>
                Source
              </a>
            </TH>
          </TR>
        </THEAD>
        <xsl:for-each select="LINES/ZTIMEM_LINE">
          <TR>
            <xsl:attribute name="class">versionfirstrow</xsl:attribute>
            <TD class="num"><xsl:value-of select="number(LINE_NUM)"/></TD>
            <TD class="code"><PRE><xsl:value-of select="SOURCE"/></PRE></TD>
          </TR>
        </xsl:for-each>
      </table>
    </xsl:for-each>
  </xsl:template>

  <xsl:template name="stats">
    <xsl:param name="node" select="." />
    <h2 class="stats">
      <a>
        <xsl:attribute name="name">stats</xsl:attribute>
        Statistics
      </a>
    </h2>
    <xsl:for-each select="STATS">
      <table>
        <tr>
          <td>Total lines:</td>
          <td>
            <xsl:value-of select="TOTAL_LINES"/>
          </td>
        </tr>
        <tr>
          <td>SLOC:</td>
          <td>
            <xsl:value-of select="TOTAL_LINES - COMMENT_LINES - EMPTY_LINES"/>
            <xsl:text> </xsl:text>
            <xsl:text>(</xsl:text>
            <xsl:value-of select="format-number(1 - ( COMMENT_LINES + EMPTY_LINES ) div TOTAL_LINES, '#%')"/>
            <xsl:text>)</xsl:text>
          </td>
        </tr>
        <tr>
          <td>Comment lines:</td>
          <td>
            <xsl:value-of select="COMMENT_LINES"/>
            <xsl:text> </xsl:text>
            <xsl:text>(</xsl:text>
            <xsl:value-of select="format-number(COMMENT_LINES div TOTAL_LINES, '#%')"/>
            <xsl:text>)</xsl:text>
          </td>
        </tr>
        <tr>
          <td>Empty lines:</td>
          <td>
            <xsl:value-of select="EMPTY_LINES"/>
            <xsl:text> </xsl:text>

                <xsl:text>(</xsl:text>
            <xsl:value-of select="format-number(EMPTY_LINES div TOTAL_LINES, '#%')"/>
            <xsl:text>)</xsl:text>
          </td>
        </tr>
        <tr>
          <td>Activity interval:</td>
          <td>
            <xsl:value-of select="DATE_OLDEST"/>
            <xsl:text> to </xsl:text>
            <xsl:value-of select="DATE_LATEST"/>
          </td>
        </tr>
      </table>
    </xsl:for-each>
  </xsl:template>

  <xsl:template name="footer">
    <xsl:param name="node" select="." />
    <div id="footer">
      <div>&amp;nbsp;</div>
      <div class="footer_text"><xsl:value-of select="FOOTER_TEXT"/></div>
      <div>&amp;nbsp;</div>
      Version
      <xsl:text> </xsl:text>
      <xsl:value-of select="VERSION"/>
      <div>&amp;nbsp;</div>
    </div>
  </xsl:template>

</xsl:transform>
